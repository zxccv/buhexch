//Притула Р.В. Начало 15.03.2021 #19402
Процедура УстановитьЗначениеДопСведенияОбъекта(Объект, НаименованиеДопСведения, Значение = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок", НаименованиеДопСведения);
	Если Значение <> Неопределено Тогда
		МенеджерЗаписи.Значение = Значение;
		МенеджерЗаписи.Записать();
	Иначе
		МенеджерЗаписи.Удалить();
	КонецЕсли;

	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПолучитьЗначениеДопСведенияОбъекта(Объект, НаименованиеДопСведения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.Свойство.Установить(ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок", НаименованиеДопСведения));
	НаборЗаписей.Прочитать();
	ВозвращаемоеЗначение = Неопределено;
	
	Если НаборЗаписей.Количество() И ЗначениеЗаполнено(НаборЗаписей[0].Значение) Тогда
		ВозвращаемоеЗначение = НаборЗаписей[0].Значение;	
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПолучитьЗначениеДопРеквизитаОбъекта(Объект,НаименованиеРеквизита) Экспорт 
	
	Для Каждого ДопРеквизит Из Объект.ДополнительныеРеквизиты Цикл
		
		Если ДопРеквизит.Свойство.Заголовок <> НаименованиеРеквизита Тогда Продолжить; КонецЕсли;
			
		Если ДопРеквизит.Свойство.ТипЗначения.СодержитТип(Тип("Строка")) 
			И ЗначениеЗаполнено(ДопРеквизит.ТекстоваяСтрока) Тогда
			Возврат ДопРеквизит.ТекстоваяСтрока;
		Иначе
			Возврат ДопРеквизит.Значение;
		КонецЕсли;

	КонецЦикла;	
		
	Возврат Неопределено;
	
КонецФункции

//!!
Функция ПолучитьВнешнююНастройку(НаименованиеНастройки,ЭтоТЗ = Истина, ОднаСтрокаВернутьСтруктуру = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройка = ХранилищеОбщихНастроек.Загрузить("НастройкиЭксклюзив",НаименованиеНастройки,,"НастройкиЭксклюзив");
		
	Если НЕ ЭтоТЗ Тогда
		Если Настройка <> Неопределено Тогда
			Возврат Настройка;
		Иначе
			ВызватьИсключение "Настройка " + НаименованиеНастройки + " не найдена";
		КонецЕсли;
	КонецЕсли;
		      	
	Если ТипЗнч(Настройка) = Тип("ТаблицаЗначений") И Настройка.Количество() > 0 Тогда		
		
		Если ОднаСтрокаВернутьСтруктуру Тогда
			структураНастройка = Новый Структура;
			Для Каждого Колонка Из Настройка.Колонки Цикл
				структураНастройка.Вставить(Колонка.Имя,Настройка[0][Колонка.Имя]);				
			КонецЦикла;
			Возврат структураНастройка;
		Иначе
			Возврат Настройка;
		КонецЕсли;
				
	Иначе
		ВызватьИсключение "Настройка " + НаименованиеНастройки + " не найдена";
	КонецЕсли;
	
КонецФункции
//Притула Р.В. Конец  15.03.2021

//Притула Р.В. Начало 28.05.2021 #23889
//обнаруженная недоработка по задаче #11843
Функция ПолучитьСуммуДокумента(ДокументОбъект, ИмяТабличнойЧасти, ТолькоОтрицательные=Ложь) Экспорт
	
	ТаблицаСуммОснования = Новый ТаблицаЗначений();
	ТаблицаСуммОснования.Колонки.Добавить("Сумма",     ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаСуммОснования.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаСуммОснования.Колонки.Добавить("СуммаНДС",  ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	новаяСтрока = ТаблицаСуммОснования.Добавить();
	СуммаДокумента = 0;
	Для каждого СтрокаДокумента Из ДокументОбъект[ИмяТабличнойЧасти] Цикл
		Если ТолькоОтрицательные И СтрокаДокумента.Сумма > 0 Тогда
			Продолжить;
		ИначеЕсли НЕ ТолькоОтрицательные И СтрокаДокумента.Сумма < 0 Тогда
			Продолжить;
		КонецЕсли;	
		СуммаДокумента = СуммаДокумента + ?(СтрокаДокумента.Сумма < 0, -СтрокаДокумента.Сумма, СтрокаДокумента.Сумма);
	КонецЦикла;	
		
	новаяСтрока.СтавкаНДС= Перечисления.СтавкиНДС.БезНДС;
	новаяСтрока.Сумма = СуммаДокумента;
	новаяСтрока.СуммаНДС = 0;
	
	Возврат ТаблицаСуммОснования;
	
КонецФункции
//Притула Р.В. Конец  28.05.2021